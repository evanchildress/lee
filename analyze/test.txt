model{


  for(g in 1:nGroups){
#overall mean length for each cohort
    mu[g]~dunif(muPriors[g,1],muPriors[g,2])

#variation on random year effect (mean effect only)
    sdYear[g]~dunif(0,20)
    tauYear[g]<-1/pow(sdYear[g],2)

#variation on random site effect (mean effect only)
    sdSite[g]~dunif(0,20)
    tauSite[g]<-1/pow(sdSite[g],2)

#within group/site/year variation (constant across site/year)
    sd[g]~dunif(0,100)
    tau[g]<-1/pow(sd[g],2)
  }

#random year effects
  for(y in 1:nYears){
    for(g in 1:nGroups){
      muY[g,y]~dnorm(mu[g],tauYear[g])T(muPriors[g,1],muPriors[g,2])

#fixed year effect on pu
      pu[g,y]~dunif(0,1)

#random site effect
      for(s in 1:nSites){
        muS[g,y,s]~dnorm(muY[g,y],tauSite[g])T(muPriors[g,1],muPriors[g,2])
      }
    }
  }

#likelihood
  for(i in 1:n){
    age[i]~dcat(pu[,year[i]])
    length[i]~dnorm(muS[age[i],year[i],site[i]],tau[age[i]])
  }

}